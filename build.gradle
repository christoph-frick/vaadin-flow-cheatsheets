plugins {
    id 'org.springframework.boot' version "${springbootGradlePluginVersion}"
    id 'com.vaadin' version "${vaadinGradlePluginVersion}"
    id 'groovy'
}

group = 'net.ofnir'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = "https://maven.vaadin.com/vaadin-prereleases"
    }
}

bootRun {
    dependsOn vaadinPrepareFrontend
    environment("SPRING_PROFILES_ACTIVE", "dev")
    systemProperties = System.properties
}

bootJar {
    dependsOn vaadinPrepareFrontend, vaadinBuildFrontend
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    implementation platform(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
    implementation platform("com.vaadin:vaadin-bom:${getAt('vaadin.version')}")
    implementation platform("org.apache.groovy:groovy-bom:${getAt('groovy.version')}")

    implementation "org.apache.groovy:groovy"

    implementation("com.vaadin:vaadin-spring-boot-starter") {
        [
            "com.vaadin.webjar",
            "org.webjars.bowergithub.insites",
            "org.webjars.bowergithub.polymer",
            "org.webjars.bowergithub.polymerelements",
            "org.webjars.bowergithub.vaadin",
            "org.webjars.bowergithub.webcomponents"
        ].each { exclude group: it }
    }

    developmentOnly platform(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
}
